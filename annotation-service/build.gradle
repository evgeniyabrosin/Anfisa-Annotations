plugins {
    id 'java'
}

group 'org.forome'
version '0.6.1.53'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://maven.corp.mobin.me/content/repositories/releases/" }
}

sourceSets {
    main {
        java.srcDir 'src/main/java'
        resources {
            srcDirs "src/main/resources"
        }
    }
    make_database {
        java.srcDir 'src/make_database/java'
        resources.srcDirs = main.resources.srcDirs + "src/make_database/resources"
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
    test {
        java.srcDir 'src/test/java'
        resources.srcDirs = main.resources.srcDirs + "src/test/resources"
        compileClasspath += main.output + make_database.output
        runtimeClasspath += main.output + make_database.output
    }
    test_integration {
        java.srcDir 'src/test_integration/java'
        resources.srcDirs = [
                "src/main/resources",
                "src/test/resources",
                "src/test_integration/resources"
        ]
        compileClasspath += main.output + make_database.output + test.output
        runtimeClasspath += main.output + make_database.output + test.output
    }
    test_perfomance {
        java.srcDir 'src/test_perfomance/java'
        resources.srcDirs = test_integration.resources.srcDirs + "src/test_perfomance/resources"
        compileClasspath += main.output + make_database.output + test.output + test_integration.output
        runtimeClasspath += main.output + make_database.output + test.output + test_integration.output
    }
    manual {
        java.srcDir 'src/manual/java'
        resources.srcDirs = test_perfomance.resources.srcDirs + "src/manual/resources"
        compileClasspath += main.output + make_database.output + test.output + test_integration.output + test_perfomance.output
        runtimeClasspath += main.output + make_database.output + test.output + test_integration.output + test_perfomance.output
    }
}

configurations {
    make_databaseCompile.extendsFrom compile
    make_databaseRuntime.extendsFrom runtime

    test_integrationCompile.extendsFrom testCompile
    test_integrationRuntime.extendsFrom testRuntime

    test_perfomanceCompile.extendsFrom test_integrationCompile
    test_perfomanceRuntime.extendsFrom test_integrationRuntime

    manualCompile.extendsFrom test_perfomanceCompile
    manualRuntime.extendsFrom test_perfomanceRuntime
}

jar {
    if (project.hasProperty('make_database')) {
        archiveName = project.hasProperty('filename') ? filename : (rootProject.name + '_v.' + project.version + '-full.jar')

        from files(sourceSets.make_database.output)
        from {
            configurations.make_databaseCompile.collect {
                it.isDirectory() ? it : zipTree(it)
            }
            configurations.make_databaseRuntime.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
    } else {
        archiveName = project.hasProperty('filename') ? filename : (rootProject.name + '_v.' + project.version + '.jar')

        from {
            configurations.compile.collect {
                it.isDirectory() ? it : zipTree(it)
            }
            configurations.runtime.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
    }

    manifest {
        attributes(
                'Main-Class': 'org.forome.annotation.Main',
                'Implementation-Version': project.version
        )
    }
}

dependencies {

    compile 'com.github.samtools:htsjdk:2.19.0'

    compile 'org.rocksdb:rocksdbjni:6.6.4'

    compile 'mysql:mysql-connector-java:5.1.47'
    compile 'com.mchange:c3p0:0.9.5.2'

    compile 'org.apache.httpcomponents:httpasyncclient:4.1.4'
    
    compile 'org.eclipse.jetty:jetty-server:9.4.11.v20180605'
    compile 'org.eclipse.jetty:jetty-servlet:9.4.11.v20180605'
    compile 'org.eclipse.jetty:jetty-webapp:9.4.11.v20180605'

    compile 'org.springframework:spring-core:4.3.9.RELEASE'
    compile 'org.springframework:spring-web:4.3.9.RELEASE'
    compile 'org.springframework:spring-webmvc:4.3.9.RELEASE'
    compile 'org.springframework:spring-context-support:4.3.9.RELEASE'
    compile 'org.springframework:spring-messaging:4.3.9.RELEASE'
    compile 'javax.servlet:jstl:1.2'

    compile 'commons-fileupload:commons-fileupload:1.3.2'

    compile "io.github.graphql-java:graphql-java-annotations:7.2.1"

    compile 'com.jcraft:jsch:0.1.55'
    compile 'org.reflections:reflections:0.9.11'
    compile 'net.minidev:json-smart:2.3'
    compile 'com.infomaximum:querypool-rdao:0.0.1'
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'commons-cli:commons-cli:1.4'
    compile 'com.google.guava:guava:23.0'
    compile 'org.apache.commons:commons-math3:3.6.1'
    compile 'org.apache.commons:commons-collections4:4.4'
    compile 'io.reactivex.rxjava2:rxjava:2.2.7'

    compile 'in.ashwanthkumar:slack-java-webhook:0.0.9'

    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'ch.qos.logback:logback-core:1.2.2'
    compile 'ch.qos.logback:logback-classic:1.2.3'

    testCompile group: 'junit', name: 'junit', version: '4.12'

    test_integrationCompile 'org.apache.httpcomponents:httpclient:4.5.2'
}

task getVersion{
    doLast {
        print project.version
    }
}